(cl:in-package #:sicl-list-hash-table)

(defmethod (setf gethash)
    (new-value key (hash-table list-hash-table) &optional default)
  (declare (ignore default))
  (with-accessors ((contents contents)
                   (test %hash-table-test)
                   (rehash-size hash-table-rehash-size)
                   (size size))
      hash-table
    (let ((entry (assoc key contents :test test)))
      (if (null entry)
          (progn
            (push (cons key new-value) contents)
            (when (> (hash-table-count hash-table)
                     (* (hash-table-rehash-threshold hash-table)
                        (hash-table-size hash-table)))
              (if (integerp rehash-size)
                  (incf size rehash-size)
                  (setf size (ceiling (* size rehash-size))))))
          (setf (cdr entry) new-value))))
  new-value)
