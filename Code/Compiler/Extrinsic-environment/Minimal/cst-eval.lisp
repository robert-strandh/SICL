(cl:in-package #:sicl-minimal-extrinsic-environment)

(defun native-compile (lambda-expression)
  (compile nil lambda-expression))

(defun compile-cst (cst environment1 environment2 system)
  (declare (ignore system))
  (let* ((form (cst:raw cst))
         (hash (sxhash form))
         (cached-value (gethash hash *form-cache*))
         (cleavir-generate-ast:*compiler* 'cl:eval)
         (dynenv-ast
           (cleavir-ast:make-dynamic-environment-ast
            '#:loader-dynamic-environment
            :policy (cleavir-env:environment-policy environment1)))
         ;; We just create the AST even when there is a cached value,
         ;; because there might be compile-time side effects that
         ;; occur during the AST generation.
         (ast (cleavir-cst-to-ast:cst-to-ast cst environment1 nil dynenv-ast)))
    (if (null cached-value)
        (let* ((ast-bis (cleavir-ast-transformations:hoist-load-time-value
                         ast dynenv-ast))
               (hir (cleavir-ast-to-hir:compile-toplevel ast-bis))
               (ignore (cleavir-hir-transformations:eliminate-typeq hir))
               (lambda-expr (translate hir environment2))
               (fun (native-compile lambda-expr)))
          (declare (ignore ignore))
          ;; Run this just for test and to check performance.
          (cleavir-hir-transformations:segregate-only hir)
          ;; Run this just for test and to check performance.
          (cleavir-simple-value-numbering:simple-value-numbering hir)
          ;; Run this for test.  It may do some good too.
          (cleavir-remove-useless-instructions:remove-useless-instructions hir)
          (maybe-cache form fun (cleavir-ir:forms hir))
          (cons fun (cleavir-ir:forms hir)))
        cached-value)))

(defmethod cleavir-env:cst-eval (cst environment1 (environment2 environment) system)
  (let ((form (cst:raw cst)))
    (cond ((and (consp form)
                (consp (cdr form))
                (null (cddr form))
                (eq (car form) 'quote))
           (cadr form))
          ((and (symbolp form)
                (nth-value 1 (sicl-global-environment:constant-variable
                              form environment1)))
           (nth-value 0 (sicl-global-environment:constant-variable
                         form environment1)))
          ((and (atom form) (not (symbolp form)))
           form)
          ((and (consp form)
                (consp (cdr form))
                (consp (cddr form))
                (null (cdddr form))
                (eq (car form) 'sicl-global-environment:function-cell)
                (eq (caddr form) 'sicl-global-environment:*global-environment*)
                (consp (cadr form))
                (consp (cdr (cadr form)))
                (null (cddr (cadr form)))
                (eq (car (cadr form)) 'quote))
           (sicl-global-environment:function-cell (cadr (cadr form))
                                                  environment2))
          ((and (consp form)
                (consp (cdr form))
                (consp (cddr form))
                (null (cdddr form))
                (eq (car form) 'sicl-global-environment:function-cell)
                (equal (caddr form) '(sicl-global-environment:global-environment))
                (consp (cadr form))
                (consp (cdr (cadr form)))
                (null (cddr (cadr form)))
                (eq (car (cadr form)) 'quote))
           (sicl-global-environment:function-cell (cadr (cadr form))
                                                  environment2))
          ((and (consp form)
                (consp (cdr form))
                (consp (cddr form))
                (null (cdddr form))
                (eq (car form) 'sicl-global-environment:variable-cell)
                (eq (caddr form) 'sicl-global-environment:*global-environment*)
                (consp (cadr form))
                (consp (cdr (cadr form)))
                (null (cddr (cadr form)))
                (eq (car (cadr form)) 'quote))
           (sicl-global-environment:variable-cell (cadr (cadr form))
                                                  environment2))
          ((and (consp form)
                (consp (cdr form))
                (consp (cddr form))
                (null (cdddr form))
                (eq (car form) 'sicl-global-environment:variable-cell)
                (equal (caddr form) '(sicl-global-environment:global-environment))
                (consp (cadr form))
                (consp (cdr (cadr form)))
                (null (cddr (cadr form)))
                (eq (car (cadr form)) 'quote))
           (sicl-global-environment:variable-cell (cadr (cadr form))
                                                  environment2))
          ((eq form 'sicl-global-environment:*global-environment*)
           environment2)
          (t
           (let ((fun-and-forms (compile-cst cst environment1 environment2 system)))
             (tie (car fun-and-forms) (cdr fun-and-forms)
                  environment1 environment2))))))
